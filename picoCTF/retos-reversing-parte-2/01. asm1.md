## Objetivo
What does asm1(0x8be) return? Submit the flag as a hexadecimal value (starting with '0x'). NOTE: Your submission for this question will NOT be in the normal flag format. [Source](https://jupiter.challenges.picoctf.org/static/66c927e32f3d7be7a62d13a7c2250943/test.S)

## Pistas
## Solución
1. Descargar el archivo y ver el contenido:
```
asm1:
        <+0>:   push   ebp
        <+1>:   mov    ebp,esp
        <+3>:   cmp    DWORD PTR [ebp+0x8],0x71c
        <+10>:  jg     0x512 <asm1+37>
        <+12>:  cmp    DWORD PTR [ebp+0x8],0x6cf
        <+19>:  jne    0x50a <asm1+29>
        <+21>:  mov    eax,DWORD PTR [ebp+0x8]
        <+24>:  add    eax,0x3
        <+27>:  jmp    0x529 <asm1+60>
        <+29>:  mov    eax,DWORD PTR [ebp+0x8]
        <+32>:  sub    eax,0x3
        <+35>:  jmp    0x529 <asm1+60>
        <+37>:  cmp    DWORD PTR [ebp+0x8],0x8be
        <+44>:  jne    0x523 <asm1+54>
        <+46>:  mov    eax,DWORD PTR [ebp+0x8]
        <+49>:  sub    eax,0x3
        <+52>:  jmp    0x529 <asm1+60>
        <+54>:  mov    eax,DWORD PTR [ebp+0x8]
        <+57>:  add    eax,0x3
        <+60>:  pop    ebp
        <+61>:  ret    
```
2. Se meten los parámetros en orden inverso, como solo es el parámetro `0x8be` se mete a la pila, además de la dirección de retorno `ret` , la pila queda como:
```
[ ret ]
[ 0x8be ]
```
3. Empieza la ejecución de la función asm1.
4. Vamos a la línea `<+0>:   push   ebp` que indica que debemos introducir el registro `ebp`, por lo que la pila queda:
```
[ ebp ] 
[ ret ]
[ 0x8be ]
```
4. Vamos a la línea `<+1>:   mov    ebp,esp` que indica que se hace un respaldo de `esp`  en tope de la pila `ebp`, por lo que la pila queda:
```
[ ebp ] <- esp <- ebp
[ ret ]
[ 0x8be ]
```
5. Como `ebp` no se mueve, para poder acceder a `ret` y al primer parámetro, se suma 0x4 y 0x8 respectivamente.
```
[ ebp ] <- esp <- ebp
[ ret ] <- ebp + 0x4
[ 0x8be ] <- ebp + 0x8
```
6. Vamos a la línea `<+3>:   cmp    DWORD PTR [ebp+0x8],0x71c` que indica que se van a comparar el valor que se encuentra en `0x8` + el apuntador `ebp`  (`0x8be = 2238`) y  `0x71c = 1820`.
7. Vamos a la línea `<+10>:  jg     0x512 <asm1+37>` que indica que va a saltar a la línea +37 solamente si en la comparación anterior es correcta (jg salta si es más grande): `0x8be > 0x71c = True`
8. La condición se cumple, así que saltamos a la línea +37.
9. Vamos a la línea `<+37>:  cmp    DWORD PTR [ebp+0x8],0x8be` que indica que se van a comparar el valor que se encuentra en `0x8` + el apuntador `ebp`  (`0x8be = 2238`) y  `0x8be = 2238`.
10. Vamos a la línea `<+44>:  jne    0x523 <asm1+54>` que indica que va a saltar a la línea +54 solamente si en la comparación anterior es correcta (jne salta si no es igual): `0x8be != 0x8be = False`
11. La condición no se cumple, así que no saltamos.
12. Vamos a la línea `<+46>:  mov    eax,DWORD PTR [ebp+0x8]` que indica que el valor de `ebp + 0x8 =  0x8be` se va a guardar en el registro eax.
```
[ ebp ] <- esp <- ebp
[ ret ] <- ebp + 0x4
[ 0x8be ] <- ebp + 0x8

Registros
[ 0x8be ] EAX
```
12. Seguimos en la línea `<+49>:  sub    eax,0x3` que indica que se le va a restar `0x3 = 3` en el valor del registro `eax = 0x8be = 2238`, el resultado es `0x8be - 0x3 = 0x8bb`.
```
[ ebp ] <- esp <- ebp
[ ret ] <- ebp + 0x4
[ 0x8be ] <- ebp + 0x8

Registros
[ 0x8bb ] EAX
```
13. Seguimos en la siguiente línea, `<+52>:  jmp    0x529 <asm1+60>`, que indica que saltemos a la línea +60.
14. Se llegó al epílogo de la función, por lo que se toma el valor del registro `eax` y se devuelve.
15. La bandera es :
0x8bb
## Notas adicionales
Direccionamiento
BYTE - 8bits
WORD - 16bi
DWORD - 32bits

A las primeras dos líneas de la función se le conoce como el prólogo de la función y a las últimas dos se le conoce como epílogo de la función.
## Referencias
https://www.cs.virginia.edu/~evans/cs216/guides/x86.html